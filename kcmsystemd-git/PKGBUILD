# Maintainer: Sarwo Hadi Setyana <sh.setyana@gmail.com>

pkgname=kcmsystemd-git
pkgver=r38.d2004fd
pkgrel=1
pkgdesc="Systemd control module for KDE"
arch=('i686' 'x86_64')
url="https://github.com/rthomsen/kcmsystemd"
license=('GPL')
depends=('kdebase-workspace' 'boost-libs')
conflicts=('kcmsystemd')
source=("$pkgname"::'git+https://github.com/rthomsen/kcmsystemd.git')
md5sums=('SKIP')

pkgver() {
	cd $pkgname
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	if [ -d build ]; then
		rm -r build
	fi
	
	mkdir -p build
	cd build
	
	cmake ../${pkgname} -DCMAKE_BUILD_TYPE=Release \
    		-DCMAKE_INSTALL_PREFIX=`kde4-config --prefix`
	make
}

package() {
	cd build
	make DESTDIR=$pkgdir install
}
