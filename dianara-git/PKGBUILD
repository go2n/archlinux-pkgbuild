# Maintainer: Sarwo Hadi Setyana <sh.setyana@gmail.com>

pkgname=dianara-git
pkgver=r1355.c1924ac
pkgrel=1
pkgdesc="Dianara is a pump.io social networking client."
arch=('i686' 'x86_64')
url="http://dianara.nongnu.org/"
license=('GPL')
depends=('qjson' 'qoauth' 'qca-ossl')
makedepends=('git')
install="dianara.install"
conflicts=('dianara')
source=("$pkgname"::'git+https://git.gitorious.org/dianara/dianara-dev.git')
md5sums=('SKIP')

pkgver() {
	cd $pkgname
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd $pkgname
	qmake-qt4
	make
}

package() {
	cd "$pkgname"

	install -Dm755 dianara "$pkgdir/usr/bin/dianara"

	install -Dm644 dianara.desktop "$pkgdir/usr/share/applications/dianara.desktop"

	install -Dm644 icon/32x32/dianara.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/dianara.png"
	install -Dm644 icon/64x64/dianara.png "$pkgdir/usr/share/icons/hicolor/64x64/apps/dianara.png"

	install -d "$pkgdir/usr/share/locale"
	install -Dm644 translations/*.qm "$pkgdir/usr/share/locale"
}
