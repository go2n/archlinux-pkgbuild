# Maintainer: Sarwo Hadi Setyana <sh.setyana@gmail.com>

pkgname=plasmate-git
pkgver=r941.5196de2
pkgrel=1
pkgdesc='A small IDE taylored for development of Plasma components, such as Widgets, Runners, Dataengines.'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/sdk/plasmate'
license=('LGPL')
depends=('kdepimlibs')
makedepends=('cmake' 'git' 'automoc4' 'boost')
conflicts=('plasmate' 'plasmate-tools-git')
source=("$pkgname"::'git://anongit.kde.org/plasmate')
md5sums=('SKIP')

pkgver() {
	cd $pkgname
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


build() {
	if [ -d build ]; then
		rm -r build
	fi
	
	mkdir -p build
	cd build

	cmake ../${pkgname} -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
		-DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` \
		-DCMAKE_BUILD_TYPE=Release
	make
}

package() {
	cd build
	make DESTDIR=${pkgdir} install
}
