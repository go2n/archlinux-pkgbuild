# Maintainer: Sarwo Hadi Setyana <sh.setyana@gmail.com>

dl_name=mirall
pkgname=owncloud-client
pkgver=1.5.4
pkgrel=1
pkgdesc="The ownCloud client software is based on Mirall and CSync"
arch=('i686' 'x86_64')
url="http://owncloud.org/sync-clients/"
license=('GPL2')
depends=('qtkeychain' 'qtwebkit' 'neon')
makedepends=('cmake')
provides=('mirall' 'ocsync')
conflicts=('mirall-git' 'ocsync')
install=owncloud-client.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("http://download.owncloud.com/desktop/stable/${dl_name}-${pkgver}.tar.bz2"
	"${pkgname}.desktop"
	)
md5sums=('9fe298c045c1a89b22105e7455694676'
         'd331e926873637a5ebeb8158402de4ea')

build() {
	if [[ -e ${srcdir}/${dl_name}-${pkgver}-build ]]; then
		rm -rf ${srcdir}/${dl_name}-${pkgver}-build
	fi
	
	mkdir ${srcdir}/${dl_name}-${pkgver}-build
	cd ${srcdir}/${dl_name}-${pkgver}-build
	
	cmake -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
	      -DCMAKE_INSTALL_PREFIX=/usr \
	      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
	      -DCMAKE_BUILD_TYPE=Release \
	      -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} ../${dl_name}-${pkgver}
	make
}

package() {
	cd ${srcdir}/${dl_name}-${pkgver}-build
	make DESTDIR=${pkgdir} install
	install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applictions/${pkgname}.desktop
}
