# Maintainer: Sarwo Hadi Setyana <sh.setyana@gmail.com>

dl_name=mirall
pkgname=owncloud-client
pkgver=1.6.1
pkgrel=1
pkgdesc="The ownCloud client software is based on Mirall and CSync"
arch=('i686' 'x86_64')
url="http://owncloud.org/sync-clients/"
license=('GPL2')
depends=('qtkeychain' 'qtwebkit' 'neon')
makedepends=('cmake')
provides=('mirall' 'ocsync')
conflicts=('mirall-git' 'ocsync')
install=owncloud-client.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("http://download.owncloud.com/desktop/stable/${dl_name}-${pkgver}.tar.bz2")
md5sums=('4e0a73e91442a68420b980084f2b34cf')

prepare() {
	if [[ -e ${srcdir}/${dl_name}-${pkgver}-build ]]; then
		rm -rf ${srcdir}/${dl_name}-${pkgver}-build
	fi
	
	mkdir ${srcdir}/${dl_name}-${pkgver}-build
}

build() {
	cd ${srcdir}/${dl_name}-${pkgver}-build
	
	cmake -DBUILD_WITH_QT4=on \
	      -DCMAKE_INSTALL_PREFIX=/usr \
	      -DCMAKE_INSTALL_LIBDIR=lib \
	      -DCMAKE_BUILD_TYPE=Release \
	      -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} ../${dl_name}-${pkgver}
	make
}

package() {
	cd ${srcdir}/${dl_name}-${pkgver}-build
	make DESTDIR=${pkgdir} install
}
