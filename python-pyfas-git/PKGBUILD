# Maintainer: Sarwo Hadi Setyana <sh.setyana@gmail.com>

pkgname=python-pyfas-git
pyname=pyfas
pkgver=r143.5bd1a2b
pkgrel=1
pkgdesc="Pyfas is a python toolbox for flow assurance engineers"
arch=(any)
url="https://github.com/gpagliuca/pyfas"
license=('GPL3')
depends=('python-sphinx' 'python-openpyxl' 'jupyter>=1.0.0' 'python-quantities>=0.11.1' 'ipython>=4.0' 'python-nbsphinx>=0.2' 'python-pandas')
makedepends=('git' 'python>=3.6')
conflicts=('python-pyfas')
source=('git+https://github.com/gpagliuca/pyfas.git')
md5sums=('SKIP')


pkgver() {
    cd "${srcdir}/${pyname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${pyname}"
    python setup.py build
}

package() {
    cd "${srcdir}/${pyname}"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
 
    install -D -m644 README.md ${pkgdir}/usr/share/doc/${pkgname}/README
    install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
